plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 31

    def appVersionCode = 21
    def appVersionName = "build_${appVersionCode} (alpha)"

    buildFeatures {
        viewBinding false
    }

    defaultConfig {
        applicationId "app.simple.inure"

        // minSdkVersion 21 is needed for pthread_mutex_timedlock and pthread_condattr_setclock
        // minSdkVersion 23 is needed for forkpty
        minSdkVersion 23
        targetSdkVersion 31
        versionCode appVersionCode
        versionName appVersionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "versionName", versionName

        ndkVersion '23.1.7779620'

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "_debug"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'

    // Google
    implementation 'com.google.android.material:material:1.5.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Toolkit
    implementation 'com.github.android:renderscript-intrinsics-replacement-toolkit:b6363490c3'

    // Third Party
    implementation 'com.jaredrummler:apk-parser:1.0.2'
    implementation 'com.scottyab:rootbeer-lib:0.0.9'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.bogdwellers:pinchtozoom:0.1'
    implementation "com.anggrayudi:storage:0.13.0"
    implementation 'com.github.duanhong169:drawabletoolbox:1.0.7'

    // root
    def libsuVersion = '3.1.2'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"

    // debugImplementation because LeakCanary should only run in debug builds.
    // debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}